plugins {
    id "java"
    id "com.modrinth.minotaur" version "2.+"
}

repositories {
    flatDir {
        dirs "canyon-api"
    }
}

dependencies {
    compileOnly "com.canyonmodded:canyon-api:1.7.3"
}

def pluginVersion = "1.2.3"

base.archivesName = "my-plugin-" + pluginVersion

def javaVersion = JavaVersion.VERSION_1_8

java {
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
}

jar.from("LICENSE.txt")

processResources {
    filesMatching("plugin.yml") {
        expand "version": pluginVersion
    }
}

modrinth {
    token = System.getenv("MINOTAUR_PAT")
    syncBodyFrom = rootProject.file("modrinth/readme.md").text
    uploadFile = jar
    projectId = "my-plugin"
    changelog = rootProject.file("modrinth/changelog.md").text
    versionName = "My Plugin " + pluginVersion
    versionNumber = pluginVersion
    gameVersions = ["b1.7.3"]
    loaders = ["bukkit"]
}

task modrinthUpload {
    dependsOn "modrinth"
    dependsOn "modrinthSyncBody"
}
